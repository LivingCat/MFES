class Sale
/*
 Define a sale
*/
instance variables
  client: Client;
  products: map Product to nat1;
  date: Date;
  grandTotal: real;
  
operations

  -- Calculates the total of the sale (sell price * quantity)
	private calculateGrandTotal: () ==> real
		calculateGrandTotal() == (
			dcl sum:real := 0;
			for all p in set dom products do sum := sum + p.getSellPrice() * products(p);
			return sum;
		);
		
  --Constructor for class Sale
  --c - client that purchased the products
  --ps - products and respective quantities that were bought
  --d - date in which the transaction occurred
	public Sale: Client * map Product to nat1 * Date ==> Sale
		Sale(c,ps,d) == (
			client := c;
			products := ps;
			date := d;
			grandTotal := calculateGrandTotal();
			return self;
		)
		pre card dom ps <> 0 --sale needs at least one product
		post grandTotal > 0; --total costs of the sale are higher than 0€
		
	-- Retrieve Client that purchased the products
	public getClient: () ==> Client
	getClient() == return client;
	
	--Retrieve Products sold
	public getProducts: () ==> map Product to nat1
	getProducts() == return products;
	
	--Retrieve transaction date
	public getDate: () ==> Date
	getDate() == return date;
	
	--Retrieve sale total
	public getGrandTotal: () ==> real
	getGrandTotal() == return grandTotal;

end Sale
