class StoreTest
/*
	Contains the test cases for the store.
*/
operations
	-- Simulates assertion checking by reducing it to pre-condition checking.
	private assertTrue: bool ==> ()
	assertTrue(cond) == return
		pre cond;
		
	-- Simulates assertion checking by reducing it to post-condition checking.
	private assertEqual: ? * ? ==> ()
	assertEqual(expected, actual) == return
		post expected = actual;
	
	-- Tests the type usage of the type date.
	private testDate: () ==> () 
	testDate() == (
		dcl date : Date;
		
		date := new Date(29,02,2020);
		assertEqual(date.getDate(),"29/02/2020");
		date := new Date(28,02,2019);
		assertEqual(date.getDate(),"28/02/2019");
		date := new Date(04,06,5789);
		assertEqual(date.getDate(),"04/06/5789");
		date := new Date(08,03,2019);
		assertEqual(date.getDate(),"08/03/2019");
	);
	
	-- Tests the creation of a store without initial cash
	private testCreateStore: () ==> ()
	testCreateStore() == (
		dcl store : Store := new Store("Kids2Kids","UK");
		assertEqual(store.getName(),"Kids2Kids");
		assertEqual(store.getCountry(),"UK");
		assertEqual(store.getCash(),0);
	);
	
	-- Tests the registry of a new product class, as specified in R7
	private testAddProductClass: () ==> ()
	testAddProductClass() == (
		dcl store : Store := new Store("Kids2Kids","UK",1000);
		dcl pc1 : ProductClass := new ProductClass("food");
		dcl pc2 : ProductClass := new ProductClass("toy");
		
		store.addProductClass(pc1);
		store.addProductClass(pc2);
		assertEqual(store.getNumberOfProductClasses(),2);
		
		assertTrue(exists1 pc in set store.getProductClasses() & pc.getName() = "food");
		assertTrue(exists1 pc in set store.getProductClasses() & pc.getName() = "toy");
	);
	
	-- Tests the removal of a product class, as specified in R8
	private testRemoveProductClass: () ==> ()
	testRemoveProductClass() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("food");
		dcl pc2 : ProductClass := new ProductClass("toy");
		dcl p1 : Product := new Product(pc1,"apple",0.1,0.2);
		dcl p2 : Product := new Product(pc2,"ball",5,6);
		dcl p3 : Product := new Product(pc1,"banana",0.2,0.3);
		
		store.addProductClass(pc1);
		store.addProductClass(pc2);
		store.addProduct(p1,3);
		store.addProduct(p2,2);
		store.addProduct(p3,3);
		
		assertEqual(store.getNumberOfProductClasses(),2);
		assertEqual(store.getNumberOfProducts(),3);
		 
		store.removeProductClass("food");
		assertEqual(store.getNumberOfProductClasses(),1);
		assertEqual(store.getNumberOfProducts(),1);
		assertTrue(not exists pc in set store.getProductClasses() & pc.getName() = "food");
		assertTrue(not exists p in set dom store.getStoreProducts() & p.getClass().getName() = "food");
	);
	
	-- Tests the registry of a new product, as specified in R4
	private testAddProduct: () ==> ()
	testAddProduct() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl p1 : Product := new Product(pc1,"doll",5,6);
		dcl products: map Product to nat := {|->};
		
		store.addProductClass(pc1);
		store.addProduct(p1,3);
	--check if we have 1 more product in the product map
		assertEqual(store.getNumberOfProducts(),1);
		assertTrue(exists1 p in set dom store.getStoreProducts() & p.getName() = "doll");
		
		products := store.getStoreProducts();
	--check if we have the right cardinality of the product in the product map
		for all p in set dom products do if (p.getName() = p1.getName()) then assertEqual(3,products(p)); 
		
	);
	
	-- Tests the removal of a product, as specified in R5
	private testRemoveProduct: () ==> ()
	testRemoveProduct() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl p1 : Product := new Product(pc1,"doll",5,6);
		dcl p2 : Product := new Product(pc1,"ball",5,6);
		
		store.addProductClass(pc1);
		store.addProduct(p1,3);
		store.addProduct(p2,2);
		
		--remove ball
		store.removeProduct("ball");
		assertTrue(not exists p in set dom store.getStoreProducts() & p.getName() = "ball");
		store.removeProduct("doll");
		assertTrue(not exists p in set dom store.getStoreProducts() & p.getName() = "doll");
		--check if the store still has the "toy" class
		assertEqual(store.getNumberOfProductClasses(),1);
		--check if store has 0 products
		assertEqual(store.getNumberOfProducts(),0);

	);
	
	-- Test the addition of stock to a product, as specified in R6
	private testAddStock: () ==> ()
	testAddStock() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("food");
		dcl p1 : Product := new Product(pc1,"apple",0.1,0.2);
		dcl p2 : Product := new Product(pc1,"banana",0.2,0.3);
		dcl products: map Product to nat := {|->};
		
		store.addProductClass(pc1);
		--store add 2 apples
		store.addProduct(p1,2);
		--now store has 4 more apples
		store.addStock(p1,4);
		--store add 0 bananas
		store.addProduct(p2,0);
		--now store has 3 bananas
		store.addStock(p2,3);
		
		products := store.getStoreProducts();
		for all p in set dom products do if (p.getName() = p1.getName()) then assertEqual(6,products(p)); 
		for all p in set dom products do if (p.getName() = p2.getName()) then assertEqual(3,products(p)); 
	);
	
	-- Test the registry of a new purchase from a Client, as specified in R2
	private testBuyFromClient: () ==> ()
	testBuyFromClient() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl p1 : Product := new Product(pc1,"doll",0.1,0.2);
		dcl p2 : Product := new Product(pc1,"cube",0.2,0.3);
		dcl bought : map Product to nat1 := {p1|->2, p2 |-> 3};
		dcl client : Client := new Client("maria");
		dcl date : Date := new Date(30,12,2018);
		dcl purchase: PurchaseFromClient;
		dcl products: map Product to nat := {|->};
		dcl cash: real := store.getCash();
		
		store.addProductClass(pc1);
		store.addProduct(p1,1);
		store.addProduct(p2,0);
		
		purchase := store.buy(bought,client,date);
		
		--check if grand total was calculated correctly
		assertEqual(purchase.getGrandTotal(),0.8);
		
		--check if we have a new purchase in the purchase map
		assertEqual(store.getNumberOfPurchases(),1);
		assertTrue(exists1 p in set store.getPurchases() & isofclass(PurchaseFromClient,p) => narrow_(p,PurchaseFromClient).getClient().getName() = "maria");
		assertTrue(exists1 p in set store.getPurchases() & p.getDate().getDate() = "30/12/2018");
		assertTrue(exists1 p in set store.getPurchases() & card ({p1,p2} inter dom p.getProducts()) = 2);
		
		--check if the products quantity increased
		products := store.getStoreProducts();
		for all p in set dom products do if(p.getName() = p1.getName() or p.getName() = p2.getName()) then assertEqual(3,products(p));
		--check if cash decreased
		assertEqual(store.getCash(),cash - 0.8);
	);
	
	-- Test the registry of a new purchase from a Supplier, as specified in R3
	private testBuyFromSupplier: () ==> ()
	testBuyFromSupplier() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl pc2 : ProductClass := new ProductClass("food");
		dcl p1 : Product := new Product(pc1,"doll",0.1,0.2);
		dcl p2 : Product := new Product(pc2,"apple",0.1,0.2);
		dcl bought : map Product to nat1 := {p1|->2, p2 |-> 3};
		dcl supplier : Supplier := new Supplier("joao");
		dcl date : Date := new Date(30,12,2018);
		dcl purchase: PurchaseFromSupplier;
		dcl products: map Product to nat := {|->};
		dcl cash: real := store.getCash();
		
		store.addProductClass(pc1);
		store.addProductClass(pc2);
		store.addProduct(p1,1);
		store.addProduct(p2,0);
		products := store.getStoreProducts();
		assertTrue(products(p1) = 1);
		assertTrue(products(p2) = 0);
		
		purchase := store.buy(bought,supplier,date);
		
		--check if grand total was calculated correctly
		assertEqual(purchase.getGrandTotal(),0.5);
		
		--check if we have a new purchase in the purchase map
		assertTrue(store.getNumberOfPurchases() = 1);
		assertTrue(exists1 p in set store.getPurchases() & isofclass(PurchaseFromSupplier,p) => narrow_(p,PurchaseFromSupplier).getSupplier().getName() = "joao");
		assertTrue(exists1 p in set store.getPurchases() & p.getDate().getDate() = "30/12/2018");
		assertTrue(exists1 p in set store.getPurchases() & card ({p1,p2} inter dom p.getProducts()) = 2);
		
		--check if the products quantity increased
		products := store.getStoreProducts();
		
		for all p in set dom products do if(p.getName() = p1.getName() or p.getName() = p2.getName()) then assertEqual(3,products(p));
		--check if cash decreased
		assertEqual(store.getCash(),cash - 0.5);
	);
	
	-- Test the registry of a new sale from a Client, as specified in R1
	private testSell: () ==> ()
	testSell() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl pc2 : ProductClass := new ProductClass("food");
		dcl p1 : Product := new Product(pc1,"doll",0.1,0.2);
		dcl p2 : Product := new Product(pc2,"apple",0.1,0.2);
		dcl sold : map Product to nat1 := {p1|->1, p2 |-> 1};
		dcl client : Client := new Client("joao");
		dcl date : Date := new Date(30,12,2018);
		dcl sale: Sale;
		dcl products: map Product to nat := {|->};
		dcl cash: real := store.getCash();
		
		store.addProductClass(pc1);
		store.addProductClass(pc2);
		store.addProduct(p1,5);
		store.addProduct(p2,5);
		products := store.getStoreProducts();
		
		sale := store.sell(sold,client,date);
		
		--check if grand total was calculated correctly
		assertEqual(sale.getGrandTotal(),0.4);
		
		--check if we have a new sale in the sale set
		assertTrue(store.getNumberOfSales() = 1);
		assertTrue(exists1 s in set store.getSales() & s.getClient().getName() = "joao");
		assertTrue(exists1 s in set store.getSales() & s.getDate().getDate() = "30/12/2018");
		assertTrue(exists1 s in set store.getSales() & card ({p1,p2} inter dom s.getProducts()) = 2);
		
		--check if the products quantity decreased
		products := store.getStoreProducts();
		
		for all p in set dom products do if(p.getName() = p1.getName() or p.getName() = p2.getName()) then assertEqual(4,products(p));
		--check if cash increased
		assertEqual(store.getCash(),cash + 0.4);
	);
	
	-- Test the report functionality, as specified in R9
	private testReportMetrics: () ==> ()
	testReportMetrics() == (
		dcl store : Store := new Store("Kids2Kids","PT",1000);
		dcl pc1 : ProductClass := new ProductClass("toy");
		dcl p1 : Product := new Product(pc1,"doll",10,15);
		dcl p2 : Product := new Product(pc1,"car",15,20);
		dcl sold : map Product to nat1 := {p1|->2, p2 |-> 1};
		dcl bought : map Product to nat1 := {p1 |-> 3, p2 |-> 2};
		dcl client : Client := new Client("joao");
		dcl supplier : Supplier := new Supplier("catarina");
		dcl date : Date := new Date(30,12,2018);
		dcl sale: Sale;
		dcl purchase: Purchase;
		
		store.addProductClass(pc1);
		store.addProduct(p1,10);
		store.addProduct(p2,10);
		
		assertEqual(store.getAssets(),350);
		
		sale := store.sell(sold,client,date);
		purchase := store.buy(bought,supplier,date);
		purchase := store.buy(bought,client,date);
		
		assertEqual(store.getNumberOfSales(), 1);
		assertEqual(store.getNumberOfPurchases(), 2);
		assertEqual(store.getNumberOfClientPurchases(), 1);
		assertEqual(store.getNumberOfSupplierPurchases(), 1);
		
		assertEqual(store.getNetSales(), 50);
		assertEqual(store.getCostOfGoodsSold(), 35);
		assertEqual(store.getGrossProfit(), 15); 
	);

	-- Function that executes all the test suites
	public static main: () ==> ()
	main() == (
		dcl testSuite : StoreTest := new StoreTest();
		
		testSuite.testDate();
		testSuite.testCreateStore();
		testSuite.testAddProductClass();
		testSuite.testRemoveProductClass();
		testSuite.testAddProduct();
		testSuite.testRemoveProduct();
		testSuite.testAddStock();
		testSuite.testBuyFromClient();
		testSuite.testBuyFromSupplier();
		testSuite.testSell();
		testSuite.testReportMetrics();
	);
	
	
	--------------------------Invalid tests -----------------------------
	
	-- try to buy a product that doesn't yet exist in the store
	public testBuyProductNonExistent: () ==> ()
		testBuyProductNonExistent() == (
			dcl store : Store := new Store("Kids2Kids","PT",1000);
			dcl pc1 : ProductClass := new ProductClass("toy");
			dcl p1 : Product := new Product(pc1,"doll",0.1,0.2);
			dcl p2 : Product := new Product(pc1,"cube",5,10);
			dcl client : Client := new Client("joao");
			dcl date : Date := new Date(30,12,2018);
			dcl buy : map Product to nat1 := {p2|->1};
			dcl purchase: PurchaseFromClient;
		
			store.addProduct(p1,15);

			purchase := store.buy(buy,client,date); --breaks the pre condition
	);
	
	-- try to buy more products than the cash the store has
	public testNotEnoughMoney: () ==> ()
		testNotEnoughMoney() == (
			dcl store : Store := new Store("Kids2Kids","PT");
			dcl pc1 : ProductClass := new ProductClass("toy");
			dcl p1 : Product := new Product(pc1,"doll",5,10);
			dcl client : Client := new Client("joao");
			dcl date : Date := new Date(30,12,2018);
			dcl buy : map Product to nat1 := {p1|->5};
			dcl purchase: PurchaseFromClient;
		
			store.addProductClass(pc1);
			store.addProduct(p1,2);
			
			purchase := store.buy(buy,client,date); --breaks the pre condition
	);
	
	-- try to sell more than we have in stock
	public testSellTooMuch: () ==> ()
	testSellTooMuch() == (
			dcl store : Store := new Store("Kids2Kids","PT");
			dcl pc1 : ProductClass := new ProductClass("toy");
			dcl p1 : Product := new Product(pc1,"doll",5,10);
			dcl client : Client := new Client("joao");
			dcl date : Date := new Date(30,12,2018);
			dcl sell : map Product to nat1 := {p1|->5};
			dcl sale: Sale;
		
			store.addProductClass(pc1);
			store.addProduct(p1,2);
			
			sale := store.sell(sell,client,date); --breaks the pre condition
			
	);
	
	-- try to sell a product that doesn't exist in the store
		public testSellNonExistent: () ==> ()
			testSellNonExistent() == (
				dcl store : Store := new Store("Kids2Kids","PT");
				dcl pc1 : ProductClass := new ProductClass("toy");
				dcl p1 : Product := new Product(pc1,"doll",5,10);
				dcl p2 : Product := new Product(pc1,"cube",2,3);
				dcl client : Client := new Client("joao");
				dcl date : Date := new Date(30,12,2018);
				dcl sell : map Product to nat1 := {p2|->5};
				dcl sale: Sale;
			
				store.addProductClass(pc1);
				store.addProduct(p1,2);
			
				sale := store.sell(sell,client,date); --breaks the pre condition
			
	);
	
	-- try to add an already existing product to the store
		public testAlreadyExistentProduct: () ==> ()
			testAlreadyExistentProduct() == (
				dcl store : Store := new Store("Kids2Kids","PT");
				dcl pc1 : ProductClass := new ProductClass("toy");
				dcl p1 : Product := new Product(pc1,"doll",5,10);
				dcl p2 : Product := new Product(pc1,"doll",2,3);
			
				store.addProductClass(pc1);
				store.addProduct(p1,2);
				store.addProduct(p2,3);			--breaks pre condition
	);
	
		-- try to remove a product that doesnt exist in the store
		public testRemoveNonExistentProduct: () ==> ()
			testRemoveNonExistentProduct() == (
				dcl store : Store := new Store("Kids2Kids","PT");
				dcl pc1 : ProductClass := new ProductClass("toy");
				dcl p1 : Product := new Product(pc1,"doll",5,10);
			
				store.addProductClass(pc1);
				store.removeProduct(p1.getName());	--breaks pre condition
		);
	
		-- try to add an already existing product class to the store
		public testAlreadyExistentProductClass: () ==> ()
			testAlreadyExistentProductClass() == (
				dcl store : Store := new Store("Kids2Kids","PT");
				dcl pc1 : ProductClass := new ProductClass("toy");
				dcl pc2 : ProductClass := new ProductClass("toy");
			
				store.addProductClass(pc1);
				store.addProductClass(pc2); --breaks pre condition
	);
	
		-- try to remove a product class that doesnt exist in the store
		public testRemoveNonExistentProductClass: () ==> ()
			testRemoveNonExistentProductClass() == (
				dcl store : Store := new Store("Kids2Kids","PT");
				dcl pc1 : ProductClass := new ProductClass("toy");
			
				store.removeProductClass(pc1.getName());	--breaks pre condition
		);
	
end StoreTest